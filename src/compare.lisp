;; -*- mode: lisp; syntax: common-lisp; coding: utf-8-unix; package: cdr-8; -*-

(in-package #:cdr-8)

(defgeneric compare (a b &key &allow-other-keys))

(defmethod compare ((a T) (b T) &key &allow-other-keys)
  '/=)

(defmethod compare ((a number) (b number) &key &allow-other-keys)
  (cond
   ((= a b) '=)
   ((and (realp a) (realp b))
    (if (< a b) '< '>))
   (T '/=)))

(defmethod compare ((a character) (b character) &key case-sensitive &allow-other-keys)
  (if case-sensitive
      (cond
       ((char= a b) '=)
       ((char< a b) '<)
       (T '>))
      (cond
       ((char-equal a b) '=)
       ((char-lessp a b) '<)
       (T '>))))

(defmethod compare ((a string) (b string) &key case-sensitive &allow-other-keys)
  (if case-sensitive
      (cond
       ((string= a b) '=)
       ((string< a b) '<)
       (T '>))
      (cond
       ((string-equal a b) '=)
       ((string-lessp a b) '<)
       (T '>))))

(defmethod compare ((a symbol) (b symbol) &key &allow-other-keys)
  (if (eq a b) '= '/=))

(define-condition uncomparable-objects-error ()
  ((objects :initarg :objects :reader uncomparable-objects-error-objects))
  (:report (lambda (condition stream)
             (format stream "Uncomparable objects［誉犷誉夯利誉［犷濑夯蔺蔺躅泔眇狎徕戾镡赍泗蟓弪蝻颦镡赍泗泔钿轸轱瞟┅┅ㄤ彐躅躅泔眇狎徕戾ㄡ猢ㄥ蝌矧躅泔眇狎徕戾镡赍泗蟓弪蝻猴怅邈趔扉篝猢┅ㄤ彐躅祠ㄡ蝈篝脲脲犰祜鳝雉桢颦脲螬ㄣ狍ㄡ痧禊＇泔眇狎脲螬冤è京紊泰ㄔ躅泔眇狎徕戾猢┅ㄤ彐躅祠ㄡ蝈篝脲脲犰祜鳝雉桢颦脲螬ㄣ狍ㄡ痧禊＇泔眇狎脲螬è僵冤紊泰ㄔ躅泔眇狎徕戾猢┅ㄤ彐躅玺ㄡ蝈篝脲脲犰祜鳝雉桢颦脲螬ㄣ狍ㄡ痧禊＇泔眇狎脲螬冤è缉紊泰ㄔ躅泔眇狎徕戾猢┅ㄤ彐躅玺ㄡ蝈篝脲脲犰祜鳝雉桢颦脲螬ㄣ狍ㄡ痧禊＇泔眇狎脲螬è僵冤紊泰ㄔ躅泔眇狎徕戾猢┅ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠箦翩ㄦ溴骈铋糸镱ъ弩箴＇祠ㄦ溴骈铋糸镱ь雉珧遽翦蝠＇祠ㄦ溴骈铋糸镱х蝈狒弪皓＇玺ㄦ溴骈铋糸镱ь雉戾篌皓＇玺濠